/* In this assignment, and all future ones, we use Gradle, a
 * build manager.
 *
 * The purpose of build managers like Gradle is to standardize and simplify
 * the process of managing a Java project. This is useful because different
 * IDEs like Eclipse or IntelliJ all have different standards and conventions
 * on how they expect their projects to be structured and organized.
 *
 * Having to manage settings for n different IDEs can be a pain, so we
 * simplify by telling them to all use Gradle instead.
 *
 * Other examples of build managers include Ant and Maven.
 *
 * You do not need to know anything about Gradle or build managers to complete
 * this project, but if you're curious and want to learn more, take a look
 * at guides and documentation on the Gradle website:
 * https://guides.gradle.org/creating-new-gradle-builds/
 * https://docs.gradle.org/current/userguide/tutorial_using_tasks.html
 * https://docs.gradle.org/current/userguide/building_java_projects.html
 */

// Here, we list some libraries that we use to augment this build script
// itself.
plugins {
    // Use the Java plugin to add support for Java
    id 'java'

    // Use IDE plugins so we can generate IDE project files
    id 'eclipse'
    id 'idea'

    id 'checkstyle'
    id 'com.adarshr.test-logger' version '1.6.0'

    // Use ANTLR plugin for parsing calculator input
    id 'antlr'
    // Use application plugin to make this a command-line app
    id 'application'
}

// This sets the version of Gradle we'll be using.
wrapper {
    gradleVersion = '5.3.1'
    distributionType = Wrapper.DistributionType.BIN
}

// This project uses a few 3rd party libraries. Rather than downloading
// and installing them manually, which can be highly error-prone, we
// have Gradle do it for us. In the "repositories" section, we specify
// where we want to search for these 3rd party packages.
repositories {
    // Maven central is a website containing a large collection of
    // 3rd party libraries. It was originally meant to be used with the
    // Maven build tool, but it's so comprehensive other build tools decided
    // to just use this one instead of building a similar website.
    //
    // mavenLocal() refers a folder in your computer that contains any
    // libraries you may have already pre-downloaded; mavenCentral refers to
    // the actual website.
    mavenLocal()
    mavenCentral()
}

// Here, we list all the different libraries we plan on using.
// Gradle will automatically download them from the repositories listed above.
dependencies {
    // We use antlr to help us generate a 'grammar' and a 'parser' for
    // our calculator language.
    antlr group: 'org.antlr', name: 'antlr4', version: '4.5.1'
    compile group: 'org.antlr', name: 'antlr4-runtime', version: '4.5.1'

    // We use jfreechart to help us chart graphs and other visualizations.
    compile group: 'org.jfree', name: 'jfreechart', version: '1.0.19'

    // We use jol to help us extract the approximate size of Java objects
    compile group: 'org.openjdk.jol', name: 'jol-core', version: '0.9'

    // We use jUnit to help us write tests.
    testCompile group: 'junit', name: 'junit', version: '4.12'
}

// Configure ANTLR plugin
generateGrammarSource {
    maxHeapSize = '64m'
    arguments += [
            '-Werror',
            '-visitor',
            '-long-messages',
            '-package', 'calculator.parser.grammar'
    ]
    outputDirectory = new File('src/main/java/calculator/parser/grammar/')
}

// Configure main class for command line
application {
    mainClassName = "calculator.Main"
}

// Configure how we run code
run {
    standardInput = System.in
}

// Generate javadocs
task generateDocs(type: Javadoc) {
    source = sourceSets.main.allJava
    classpath = sourceSets.main.compileClasspath
    options.links('http://docs.oracle.com/javase/8/docs/api/')
}

// Enable warnings
allprojects {
    gradle.projectsEvaluated {
        tasks.withType(JavaCompile) {
            options.compilerArgs << "-Xlint:unchecked" << "-Xlint:deprecation"
        }
    }
}

// Configure pretty test logger
testlogger {
    theme 'standard'
    showExceptions false
    showSummary false
    slowThreshold 60000
    showStandardStreams false
    showPassedStandardStreams false
    showSkippedStandardStreams false
    showFailedStandardStreams false
}

// Define profiles for testing separate parts of the assignment
test {
    def profileString = System.properties['test.profile'] ?: ''
    def profiles = profileString.split(',')
    if ('hw2p1' in profiles || 'hw2' in profiles) {
        include 'datastructures/TestDoubleLinkedList*'
        include 'datastructures/TestDoubleLinkedListDelete*'
        include 'datastructures/dictionaries/TestArrayDictionary*'
    }
    if ('hw2p2' in profiles || 'hw2' in profiles) {
        include 'calculator/TestCalculator*'
    }
    if ('custom' in profiles) {
        include '**/Test*Custom*'
    }

    reports {
        html.enabled = false
        junitXml.enabled = false
    }
}

checkstyle {
    toolVersion = '8.2'
}

// Define profiles for checking style for separate parts of the assignment
checkstyleMain {
    def profileString = System.properties['test.profile'] ?: ''
    def profiles = profileString.split(',')
    if ('hw2p1' in profiles || 'hw2' in profiles) {
        include 'datastructures/concrete/DoubleLinkedList.java'
        include 'datastructures/concrete/dictionaries/ArrayDictionary.java'
    }
    if ('hw2p2' in profiles || 'hw2' in profiles) {
        include 'calculator/ast/ExpressionManipulators.java'
    }
    reports {
        html.enabled = false
        xml.enabled = false
    }
}

checkstyleTest {
    def profileString = System.properties['test.profile'] ?: ''
    def profiles = profileString.split(',')
    def included = false
    if ('hw2p1' in profiles || 'hw2' in profiles) {
        include 'datastructures/TestDoubleLinkedListDelete.java'
        included = true
    }
    if (!included && ('hw2p2' in profiles)) {
        exclude "*"
    }
    reports {
        html.enabled = false
        xml.enabled = false
    }
}

